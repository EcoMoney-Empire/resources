
Save New Duplicate & Edit Just Text
# Mr Message
# Version 1.0

options:

	# Variables:
	# {_sender}, {_recipient}, {_message}
	
	message sent: &8[&7To &c%{_recipient}%&8]: &f%{_message}%
	message received: &8[&7From &b%{_sender}%&8]: &f%{_message}%

	msg command: //msg
	msg aliases: //message, //m
	reply command: //r
	reply aliases: //reply
	
	cooldown: 3 seconds
	cooldown message: &cThat command is on cooldown.
	
	override permission: simple.msgo
	
	msgs disabled: &cThat player has messages disabled.
	msg usage: &c//msg <player> <message>
	reply usage: &c//r <message>
	no one to reply: &cYou have no one to reply to.
	player offline: &cThat player is offline.
	
	messages enabled: &aSuccessfully enabled messages.
	messages disabled: &cSuccessfully disabled messages.
	
#
#
	
function msg(sender: player, recipient: player, message: text):
	send "{@message sent}" to {_sender}
	send "{@message received}" to {_recipient}
	set {sender::%{_recipient}%} to {_sender}

command {@msg command} [<offline player>] [<text>]:
	cooldown: {@cooldown}
	cooldown message: {@cooldown message}
	aliases: {@msg aliases}
	trigger:
		if arg 2 isn't set:
			send "{@msg usage}"
			cancel cooldown
		else:
			if arg 1 is offline:
				send "{@player offline}"
				cancel cooldown
			else:
				if player has permission "{@override permission}":
					msg(player, arg 1, arg 2)
				else:
					if {msg::%arg 1%} is set:
						send "{@msgs disabled}"
						cancel cooldown
					else:
						msg(player, arg 1, arg 2)
						
command {@reply command} [<text>]:
	cooldown: {@cooldown}
	cooldown message: {@cooldown message}
	aliases: {@reply aliases}
	trigger:
		if arg isn't set:
			send "{@reply usage}"
			cancel cooldown
		else:
			set {_recipient} to {sender::%player%}
			if {_recipient} is offline:
				delete {sender::%player%}
				delete {_recipient}
			if {_recipient} isn't set:
				send "{@no one to reply}"
				cancel cooldown
			else:
				msg(player, {_recipient}, arg)
			
command /msgtoggle:
	trigger:
		if {msg::%player%} isn't set:
			set {msg::%player%} to true
			send "{@messages enabled}"
		else:
			delete {msg::%player%}
			send "{@messages disabled}"
			
